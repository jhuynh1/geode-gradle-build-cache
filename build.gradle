plugins {
    id 'java'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

sourceCompatibility = 1.8

dependencies {
    compile 'org.apache.geode:geode-core:1.11.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}


task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


def pomConfig = {
    licenses {
        license {
            name "MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "jhuynh1"
            name "Jason Huynh"
        }
    }

    scm {
        url "https://github.com/jhuynh1/geode-gradle-build-cache"
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId = 'com.github.jhuynh1.geode.gradle.build.cache'
            artifactId = 'geode-gradle-build-cache'
            version = '0.1'
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'geode-gradle-build-cache')
                root.appendNode('url', 'https://github.com/jhuynh1/geode-gradle-build-cache')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']

    pkg {
        repo = 'jhuynh1-maven'
        name = 'GeodeGradleBuildCache'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/jhuynh1/geode-gradle-build-cache.git'
        version {
            name = '0.1'
            released  = new Date()
        }
    }
}

